
dependencies {
	api project(':main:boofcv-ip')
	api project(':main:boofcv-io')
	api project(':main:boofcv-feature')
	api project(':main:boofcv-geo')
	api project(':main:boofcv-sfm')
	api project(':main:boofcv-recognition')
	api project(':demonstrations')
	api project(':integration:boofcv-swing')
	api project(':integration:boofcv-WebcamCapture')
	api project(':integration:boofcv-pdf')
	implementation files('libs/webcam-capture-0.3.13-20200330.202351-7.jar', 'libs/bridj-0.7-20140918-3.jar', 'libs/slf4j-api-1.7.2.jar')
	api "args4j:args4j:$args4j_version"
}

// Create a jar which contains all the applications
task applicationsJar(type: Jar) {
	manifest {
		attributes 'Implementation-Title': 'BoofCV Applications Jar',
				'Implementation-Version': project.version,
				'Main-Class': 'boofcv.app.Help'
	}

	outputs.upToDateWhen { false }

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	archiveFile.set(file('applications.jar'))

	from sourceSets.main.output

	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } } {
		exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
	}
}
